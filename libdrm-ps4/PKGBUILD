# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libdrm-ps4
_pkgname=libdrm
pkgver=2.4.110
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services (ps4 version)"
url="https://dri.freedesktop.org/"
arch=(x86_64)
license=('custom')
depends=('libpciaccess')
makedepends=('python-docutils' 'meson')
checkdepends=('cairo')
provides=('libdrm')
conflicts=('libdrm')
source=(https://dri.freedesktop.org/$_pkgname/$_pkgname-$pkgver.tar.xz
        libdrm.patch
        COPYING)
sha512sums=('52f92ef1fe4c218a1d7dba53ef43334dbfca80e3209afe59f3a32c4bf67473126534e990df07a931a12d46a3b997c21ef17c1c4d8a0c88d44d5c6c040e3b6be3'
            'SKIP'
            'b0ca349b882a4326b19f81f22804fabdb6fb7aef31cdc7b16b0a7ae191bfbb50c7daddb2fc4e6c33f1136af06d060a273de36f6f3412ea326f16fa4309fda660')

prepare() {
    cd $_pkgname-$pkgver
    patch -Np1 < ../libdrm.patch
}

build() {
  arch-meson $_pkgname-$pkgver build \
    -Dudev=false
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 COPYING
}
